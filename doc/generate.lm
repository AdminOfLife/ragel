#
#   Copyright 2012 Adrian Thurston <thurston@complang.org>
#

#   This file is part of Ragel.
#
#   Ragel is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
#
#   Ragel is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
#
#   You should have received a copy of the GNU General Public License
#   along with Ragel; if not, write to the Free Software
#   Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA 


lex top 
{
	rl hws /[ \t]+/

	token pd /( [^.\n]+  | '.' )/
	token nl / '\n'/

	token cmd_verb1 /'.verb|'/
	token cmd_verb2 /'.verb/'/
	token cmd_label /'.label{'/
	token cmd_ref /'.ref{'/
	token cmd_em /'.em{'/
	token cmd_tt /'.tt{'/

	token cmd_title /'.title' hws/
	token cmd_sub_title /'.subtitle' hws/
	token cmd_author /'.author' hws/

	token cmd_chapter /'.chapter' hws/
	token cmd_section /'.section' hws/
	token cmd_sub_section /'.subsection' hws/
	token cmd_sub_sub_section /'.subsubsection' hws/

	token cmd_graphic /'.graphic' hws/
	token cmd_comment /'.comment' hws? '\n'/
	token cmd_verbatim /'.verbatim' hws? '\n'/
	token cmd_code /'.code' hws? '\n'/

	token cmd_itemize /'.itemize' hws? '\n'/
	token end_itemize /'.end' hws 'itemize' hws? '\n'/
	token cmd_item /'.item' hws/

	token cmd_center /'.center' hws? '\n'/
	token end_center /'.end' hws 'center' hws? '\n'/

	token cmd_tabular /'.tabular' hws? '\n'/
	token cmd_row /'.row' hws/
	token end_tabular /'.end' hws 'tabular' hws? '\n'/

	token cmd_multicols /'.multicols' hws? '\n'/
	token cmd_columnbreak /'.columnbreak' hws? '\n'/
	token end_multicols /'.end' hws 'multicols' hws? '\n'/

	token cmd_figure / '.figure' hws?/
	token cmd_caption / '.caption' hws/
	token end_figure / '.end' hws 'figure' hws? '\n'/

	token cmd_list /'.list' hws? '\n'/
	token end_list /'.end' hws 'list' hws? '\n'/
	token cmd_li /'.li' hws/

	token cmd_license /'.license' hws? '\n'/
}

lex inline_cmd
{
}

lex cmd_bar
{
	token bar_data /[^|]*/
	token end_bar /'|'/
}

lex cmd_slash
{
	token slash_data /[^/]*/
	token end_slash /'/'/
}

lex cmd_curly
{
	token curly_data /[^}]*/
	token end_curly /'}'/
}

def pl_data
	[cmd_verb1 bar_data end_bar]
|	[cmd_verb2 slash_data end_slash]
|	[cmd_label curly_data end_curly]
|	[cmd_ref curly_data end_curly]
|	[cmd_em curly_data end_curly]
|	[cmd_tt curly_data end_curly]

lex word
{
	ignore /[ \t]+/
	token word /[^ \t\n]+/
	token dw_nl /'\n'/
}

lex verbatim
{
	token end_verbatim /hws? '.' hws? 'end' hws 'verbatim' hws? '\n'/
	token verbatim_line /[^\n]* '\n'/
}

def verbatim
	[cmd_verbatim verbatim_line* end_verbatim]

lex code
{
	token end_code /hws? '.' hws? 'end' hws 'code' hws? '\n'/
	token code_line /[^\n]* '\n'/
}

def code
	[cmd_code code_line* end_code]

lex comment
{
	token end_comment /hws? '.' hws? 'end' hws 'comment' hws? '\n'/
	token comment_line /[^\n]* '\n'/
}

def comment
	[cmd_comment comment_line* end_comment]

def figure
	[cmd_figure pd nl line* caption? end_figure]

def li
	[cmd_li line2* nl]

def _list
	[cmd_list li* end_list]

def graphic
	[cmd_graphic word word? dw_nl]

def itemize
	[cmd_itemize line* item* end_itemize]

def center
	[cmd_center line* end_center]

def line2
	[pd]
|	[pl_data]

def row
	[cmd_row line2* nl]

def tabular
	[cmd_tabular row* end_tabular]

def multicols_line
	[cmd_columnbreak]
|	[line]

def multicols
	[cmd_multicols multicols_line* end_multicols]

def item
	[cmd_item line*]

def caption
	[cmd_caption line*]

def line
	[pd]
|	[nl]
|	[pl_data]
|	[comment]
|	[verbatim]
|	[code]
|	[graphic]
|	[itemize]
|	[center]
|	[tabular]
|	[multicols]
|	[figure]
|	[_list]

def sub_sub_section
	[cmd_sub_sub_section pd nl line*]

def sub_section
	[cmd_sub_section pd nl line* sub_sub_section*]

def section
	[cmd_section pd nl line* sub_section*]

def chapter
	[cmd_chapter pd nl line* section*]

def title
	[cmd_title pd nl]

def subtitle
	[cmd_sub_title pd nl]

def author
	[cmd_author pd nl]

def license
	[cmd_license line*]

def preamble_item
	[title]
|	[subtitle]
|	[author]
|	[pd]

def preamble
	[preamble_item* license]

def start 
	[preamble chapter*]

parse Start: start( stdin )
if ( ! Start ) {
	print( error() '\n' )
	exit( 1 )
}

int printPlData( Pld: pl_data )
{
	if match Pld [ cmd_verb1 V: bar_data end_bar] {
		print( '\\verb|' )
		print( V )
		print( '|' )
	}
	else if match Pld [cmd_verb2 V: slash_data end_slash] {
		print( '\\verb/' )
		print( V )
		print( '/' )
	}
	else if match Pld [cmd_label L: curly_data end_curly] {
		print( '\\label{' )
		print( L )
		print( '}' )
	}
	else if match Pld [cmd_ref L: curly_data end_curly] {
		print( '\\ref{' )
		print( L )
		print( '}' )
	}
	else if match Pld [cmd_em L: curly_data end_curly] {
		print( '{\\em ' )
		print( L )
		print( '}' )
	}
	else if match Pld [cmd_tt L: curly_data end_curly] {
		print( '{\\tt ' )
		print( L )
		print( '}' )
	}
	else {
		print( Pld )
	}
}

int printLines2( Lines: line2* )
{
	for L: line2 in repeat(Lines) {
		if match L [Pd: pd] {
			print( Pd )
		}
		if match L [PlData: pl_data] {
			printPlData( PlData )
		}
	}
}

int printLines( Lines: line* )
{
	for L: line in repeat(Lines) {
		if match L [Pd: pd] {
			print( Pd )
		}
		if match L [Nl: nl] {
			print( Nl )
		}
		if match L [PlData: pl_data] {
			printPlData( PlData )
		}
		if match L [cmd_verbatim Lines: verbatim_line* end_verbatim] {
			print( '\\begin{verbatim}\n' )
			print( Lines )
			print( '\\end{verbatim}\n' )
			print( '\\verbspace\n' )
		}
		if match L [cmd_code Lines: code_line* end_code] {
			print( '\\begin{inline_code}\n' )
			print( '\\begin{verbatim}\n' )
			print( Lines )
			print( '\\end{verbatim}\n' )
			print( '\\end{inline_code}\n' )
			print( '\\verbspace\n' )
		}
		if match L [cmd_graphic Name: word Scale: word? dw_nl] {
			print( '\\graphspace\n' )
			print( '\\begin{center}\n' )
			print( '\\includegraphics' )
			if match Scale [word]
				print( '[scale=' Scale ']' )
			else 
				print( '[scale=0.55]' )
			print( '{' Name '}\n' )
			print( '\\end{center}\n' )
			print( '\\graphspace\n' )
		}
		if match L [cmd_itemize Lines: line* Items: item* end_itemize] {
			print( '\\begin{itemize}\n' )
			printLines( Lines )
			for Item: item in repeat(Items) {
				match Item [cmd_item Lines: line*]
				print( '\\item ' )
				printLines( Lines )
			}
			print( '\\end{itemize}\n' )
		}
		if match L [cmd_figure DirData: pd nl Lines: line* Caption: caption? end_figure] {
			print( '\\begin{figure}\n' )
			print( '\\small\n' )
			printLines( Lines )
			if match Caption [cmd_caption CL: line*] {
				print( '\\caption{' )
				printLines( CL )
				print( '}\n' )
			}
			print( '\\label{' DirData '}\n' )
			print( '\\end{figure}\n' )
		}
		if match L [cmd_list LiList: li* end_list] {
			for Li: li* in LiList {
				if match Li [cmd_li Lines: line2* nl Rest: li*] {
					print( '\\noindent\\\hspace*{24pt}' )
					printLines2( Lines )
					if match Rest [ li li* ]
						print( '\\\\' )
					print( '\n' )
				}
			}
			print( '\\vspace{12pt}\n' )
		}
		if match L [cmd_center Lines: line* end_center] {
			print( '\\begin{center}\n' )
			printLines( Lines )
			print( '\\end{center}\n' )
		}
		if match L [cmd_tabular Rows: row* end_tabular] {
			print( '\\begin{tabular}{|c|c|c|}\n' )
			print( '\\hline\n' )
			for Row: row in repeat(Rows) {
				if match Row [cmd_row Lines: line2* nl ] {
					printLines2( Lines )
					print( '\\\\' '\n' )
					print( '\\hline\n' )
				}
			}
			print( '\\end{tabular}\n' )
		}
		if match L [cmd_multicols Lines: multicols_line* end_multicols] {
			print( '\\begin{multicols}{2}\n' )
			for McLine: multicols_line in repeat( Lines ) {
				if match McLine [Line: line]
					printLines( cons line* [Line] )
				else if match McLine [cmd_columnbreak] {
					print( '\\columnbreak\n' )
				}
			}
			print( '\\end{multicols}\n' )
		}
	}
}

match Start 
	[Preamble: preamble Chapters: chapter*]

Title: title = title in Preamble
match Title [cmd_title TitleData: pd nl]

SubTitle: subtitle = subtitle in Preamble
match SubTitle [cmd_sub_title SubTitleData: pd nl]

Author: author = author in Preamble
match Author [cmd_author AuthorData: pd nl]

License: license = license in Preamble
match License [cmd_license line*]


print( 
	~\documentclass[letterpaper,11pt,oneside]{book}
	~\usepackage{graphicx}
	~\usepackage{comment}
	~\usepackage{multicol}
	~\usepackage[
	~	colorlinks=true,
	~	linkcolor=black,
	~	citecolor=green,
	~	filecolor=black,
	~	urlcolor=black]{hyperref}
	~
	~\topmargin -0.20in
	~\oddsidemargin 0in
	~\textwidth 6.5in
	~\textheight 9in
	~
	~\setlength{\parskip}{0pt}
	~\setlength{\topsep}{0pt}
	~\setlength{\partopsep}{0pt}
	~\setlength{\itemsep}{0pt}
	~
	~\input{version}
	~
	~\newcommand{\verbspace}{\vspace{10pt}}
	~\newcommand{\graphspace}{\vspace{10pt}}
	~
	~\renewcommand\floatpagefraction{.99}
	~\renewcommand\topfraction{.99}
	~\renewcommand\bottomfraction{.99}
	~\renewcommand\textfraction{.01}   
	~\setcounter{totalnumber}{50}
	~\setcounter{topnumber}{50}
	~\setcounter{bottomnumber}{50}
	~
	~\newenvironment{inline_code}{\def\baselinestretch{1}\vspace{12pt}\small}{}
	~
	~\begin{document}
	~
	~\thispagestyle{empty}
	~\begin{center}
	~\vspace*{3in}
)

print( '{\\huge ' TitleData '}\\\\\n' )

print( '\\vspace*{12pt}\n' )

print( '{\\Large ' SubTitleData '}\\\\\n' )

print(
	~\vspace{1in}
	~by\\
	~\vspace{12pt}
)

print( '{\\large ' AuthorData '}\\\\\n' )

print(
	~\end{center}
	~\clearpage
	~
	~\pagenumbering{roman}
	~
	~\chapter*{License}
)

print(
	~Ragel version \version, \pubdate\\
	~Copyright \copyright\ 2003-2012 Adrian D. Thurston
	~\vspace{6mm}
	~
)

print( 
	~{\bf\it\noindent This document is part of Ragel, and as such, this document is
	~released under the terms of the GNU General Public License as published by the
	~Free Software Foundation; either version 2 of the License, or (at your option)
	~any later version.}
	~
	~\vspace{5pt}
	~
	~{\bf\it\noindent Ragel is distributed in the hope that it will be useful, but
	~WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
	~FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
	~details.}
	~
	~\vspace{5pt}
	~
	~{\bf\it\noindent You should have received a copy of the GNU General Public
	~License along with Ragel; if not, write to the Free Software Foundation, Inc.,
	~59 Temple Place, Suite 330, Boston, MA  02111-1307  USA}
)

print(
	~
	~\clearpage
	~\tableofcontents
	~\clearpage
	~
	~\pagenumbering{arabic}
)


for Chapter: chapter in repeat(Chapters) {
	match Chapter
		[cmd_chapter DirData: pd nl Lines: line* SectionList: section*]

	print( '\\chapter{' DirData '}\n' )
	printLines( Lines )

	for Section: section in repeat(SectionList) {
		match Section
			[cmd_section DirData: pd nl Lines: line* SubSectionList: sub_section*]

		print( '\\section{' DirData '}\n' )
		printLines( Lines )
		for SubSection: sub_section in repeat(SubSectionList) {
			match SubSection
				[cmd_sub_section DirData: pd nl Lines: line* 
						SubSubSectionList: sub_sub_section*]

			print( '\\subsection{' DirData '}\n' )
			printLines( Lines )

			for SubSubSection: sub_sub_section in repeat(SubSubSectionList) {
				match SubSubSection
					[cmd_sub_sub_section DirData: pd nl Lines: line*]

				print( '\\subsubsection{' DirData '}\n' )
				printLines( Lines )
			}
		}
	}
}

print( 
	~
	~\end{document}
)
