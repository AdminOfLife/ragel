rl ident
	/( alpha | '_' ) ( alpha | digit | '_' )*/

rl number
	/ digit+ /

rl hex_number
	/ '0x' [0-9a-fA-F]+ /

rl NL / '\n' /

rl c_comment 
	/ '/*' ( any | NL )* :>> '*/' /

rl cpp_comment
	/ '//' [^\n]* NL /

rl s_literal
	/ "'" ([^'\\] | NL | '\\' (any | NL))* "'" /

rl d_literal
	/ '"' ([^"\\] | NL | '\\' (any | NL))* '"' /

namespace srlex
	lex
		ignore /[\t\n ]+/
		literal '::', ';', ')'
		token word /[a-zA-Z_][a-zA-Z0-9_]*/
	end
end

namespace c_inline
	lex 
		literal 'fpc', 'fc', 'fcurs', 'ftargs',
			'fentry', 'fhold', 'fexec', 'fgoto', 'fnext',
			'fcall', 'fret', 'fbreak'

		token ident /ident/
		token number /digit+/
		token hex_number /'0x' [0-9a-fA-F]+/

		token comment
			/c_comment | cpp_comment/

		token string
			/ s_literal | d_literal /

		token whitespace
			/ ( [ \t] | NL )+ /

		literal '{', '}', '::', '*', ',', '(', ')', ';'

		token c_any / any /
	end

	def inline_expr
		[ExprItemList: expr_item*]

	def expr_item
		[ExprAny: expr_any] :ExprAny
	|	[ExprSymbol: expr_symbol] :ExprSymbol
	|	[ExprInterpret: expr_interpret] :ExprInterpret

	def expr_any
		[whitespace]
	|	[comment]
	|	[string]
	|	[number]
	|	[hex_number]
	|	[ident]
	|	[c_any]

	def expr_symbol
		[','] | ['('] | [')'] | ['*'] | ['::']

	def expr_interpret
		['fpc'] :Fpc
	|	['fc'] :Fc
	|	['fcurs'] :Fcurs
	|	['ftargs']  :Ftargs
	|	['fentry' '(' StateRef: state_ref srlex::')'] :Fentry

	def state_ref
		[OptNameSep: opt_name_sep StateRefNames: state_ref_names]

	def opt_name_sep
		[srlex::'::'] :ColonColon
	|	[] :Empty

	# List of names separated by ::
	def state_ref_names
		[StateRefNames: state_ref_names srlex::'::' Word: srlex::word] :Rec
	|	[Word: srlex::word] :Base

	def inline_block
		[BlockItemList: block_item*]
	
	def block_item
		[ExprAny: expr_any] :ExprAny
	|	[BlockSymbol: block_symbol] :BlockSymbol
	|	[BlockInterpret: block_interpret] :BlockInterpret
	|	['{' InlineBlock: inline_block '}'] :RecBlock

	def block_symbol
		[','] | [';'] | ['('] | [')'] | ['*'] | ['::']

	def block_interpret
		[ExprInterpret: expr_interpret] :ExprInterpret
	|	['fhold' whitespace? ';'] :Fhold
	|	['fgoto' whitespace? '*' InlineExpr: inline_expr ';'] :FgotoExpr
	|	['fnext' whitespace? '*' InlineExpr: inline_expr ';'] :FnextExpr
	|	['fcall' whitespace? '*' InlineExpr: inline_expr ';'] :FcallExpr
	|	['fexec' InlineExpr: inline_expr ';'] :Fexec
	|	['fgoto' StateRef: state_ref srlex::';'] :FgotoSr
	|	['fnext' StateRef: state_ref srlex::';'] :FnextSr
	|	['fcall' StateRef: state_ref srlex::';'] :FcallSr
	|	['fret' ';'] :Fret
	|	['fbreak' ';'] :Fbreak
end

namespace ragel
	lex
		ignore /[\t\n ]+/
		ignore /'#' any* :> '\n'/

		literal '^', '|', '-', ',', ':', '!', '?', '.'
		literal '(', ')', '{', '}', '*', '&', '+'

		literal '--', ':>', ':>>', '<:', '->', '**'

		literal '|*', '*|', '=>' 

		literal '@', '>', '<', '%', '$'
		literal 'from', 'to', 'eof', 'lerr', 'err'
		literal 'when', 'inwhen', 'outwhen', '>?', '$?', '%?' 

		literal ':=', '=', ';', '..'

		literal '>~', '$~', '%~', '<~', '@~', '<>~' 
		literal '>*', '$*', '%*', '<*', '@*', '<>*' 
		literal '>/', '$/', '%/', '</', '@/', '<>/' 
		literal '>!', '$!', '%!', '<!', '@!', '<>!' 
		literal '>^', '$^', '%^', '<^', '@^', '<>^' 
		literal '<>'

		literal 'machine', 'action', 'variable', 'alphtype',
				'access', 'write', 'getkey', 'export', 'import',
				'include', 'prepush', 'postpop'

		token string /
			'"' ( [^"\\] | '\\' any )* '"' |
			"'" ( [^'\\] | '\\' any )* "'"
		/

		token re_literal /
			'/' ( [^/\\] | '\\' any )* '/'
		/
		token _union /
			'[' ( [^\]\\] | '\\' any )* ']'
		/

		token word /[a-zA-Z_][a-zA-Z0-9_]*/
		token uint / number /
		token hex / hex_number /
	end 

	# Not cannot start with '{', terminated by ';', rewritten into { inline_expr }
	token _inline_expr_reparse
		/[^{;] [^;]* ';'/ {
			R: str = input.pull( match_length - 1 )
			input.pull( 1 )
			input.push( "}" )
			input.push( R )
			input.push( "{" )
		}

	def inline_expr_reparse
		[_inline_expr_reparse]
	|	[ActionExpr: action_expr]

	def join
		[Join: join ',' Expr: expression] :Rec
	|	[Expr: expression] :Base

	def expression
		[Term: term expression_op*]

	def expression_op
		['|' term]
	|	['&' term]
	|	['-' term]
	|	['--' term]

	def term
		[FactorLabel: factor_label TermOpList: term_op_list_short]

	# This list is done manually to get shortest match.
	def term_op_list_short
		[] :Empty
	|	[TermOp: term_op TermOpList: term_op_list_short] :Terms

	def term_op
		[FactorLabel: factor_label]
	|	['.' FactorLabel: factor_label]
	|	[':>' FactorLabel: factor_label]
	|	[':>>' FactorLabel: factor_label]
	|	['<:' FactorLabel: factor_label]

	def factor_label
		[word ':' factor_label]
	|	[FactorEp: factor_ep]

	def factor_ep
		[FactorAug: factor_aug]
	|	[factor_aug '->' word]

	def aug_base
		['@'] |	['>'] |	['%'] | ['$']

	def aug_op
		['@'] |	['>'] |	['%'] | ['$']
	|	['>' 'from']
	|	['>' 'to']

	def action_expr
		['{' InlineExpr: c_inline::inline_expr '}']

	def action_block
		['{' InlineBlock: c_inline::inline_block '}']

	def action_ref 
		[Word: word] :Word
	|	['(' word ')']
	|	[action_block]
	
	def priority_aug
		[uint]
	|	['+' uint]
	|	['-' uint]

	def aug_cond
		['>?'] | ['$?'] | ['%?']
	|	['>' 'when'] | ['$' 'when'] | ['%' 'when']
	|	['when'] | ['inwhen'] | ['outwhen']

	def aug_to_state
		['>~'] | ['<~'] | ['$~'] | ['%~'] | ['@~']
	|	['>' 'to'] | ['<' 'to'] | ['$' 'to'] | ['%' 'to'] | ['@' 'to']
	|	['<>~'] | ['<>' 'to']

	def aug_from_state
		['>*'] | ['<*'] | ['$*'] | ['%*'] | ['@*']
	|	['>' 'from'] | ['<' 'from'] | ['$' 'from'] | ['%' 'from'] | ['@' 'from']
	|	['<>*'] | ['<>' 'from']

	def aug_eof
		['>/'] | ['</'] | ['$/'] | ['%/'] | ['@/']
	|	['>' 'eof'] | ['<' 'eof'] | ['$' 'eof'] | ['%' 'eof'] | ['@' 'eof']
	|	['<>/'] | ['<>' 'eof']

	def aug_gbl_error
		['>!'] | ['<!'] | ['$!'] | ['%!'] | ['@!']
	|	['>' 'err'] | ['<' 'err'] | ['$' 'err'] | ['%' 'err'] | ['@' 'err']
	|	['<>!'] | ['<>' 'err']

	def aug_local_error
		['>^'] | ['<^'] | ['$^'] | ['%^'] | ['@^']
	|	['>' 'lerr'] | ['<' 'lerr'] | ['$' 'lerr'] | ['%' 'lerr'] | ['@' 'lerr']
	|	['<>^'] | ['<>' 'lerr']

	def factor_aug
		[FactorAug: factor_aug AugBase: aug_base ActionRef: action_ref] :ActionRef
	|	[factor_aug aug_base priority_aug]
	|	[factor_aug aug_base '(' word ',' priority_aug ')']
	|	[factor_aug aug_cond action_ref]
	|	[factor_aug aug_cond '!' action_ref]
	|	[factor_aug aug_to_state action_ref]
	|	[factor_aug aug_from_state action_ref]
	|	[factor_aug aug_eof action_ref]
	|	[factor_aug aug_gbl_error action_ref]
	|	[factor_aug aug_local_error action_ref]
	|	[factor_aug aug_local_error '(' word ',' action_ref ')']
	|	[FactorRep: factor_rep] :Base

	def factor_rep 
		[FactorNeg: factor_neg factor_rep_op*]

	def factor_rep_op
		['*']
	|	['**']
	|	['?']
	|	['+']
	|	['{' factor_rep_num '}']
	|	['{' ',' factor_rep_num '}']
	|	['{' factor_rep_num ',' '}']
	|	['{' factor_rep_num ',' factor_rep_num '}']

	def factor_rep_num
		[uint]

	def factor_neg 
		['!' factor_neg]
	|	['^' factor_neg]
	|	[Factor: factor]

	def factor 
		[AN: alphabet_num] :AlphabetNum
	|	[W: word] :Word
	|	[S: string] :String
	|	[U: _union] :Union
	|	[RL1: range_lit '..' RL2: range_lit] :Range
	|	[re_literal] :Regex
	|	['(' Join: join ')'] :Join

	def range_lit
		[String: string] :String
	|	[AN: alphabet_num] :AN

	def alphabet_num 
		[uint]
	|	['-' uint]
	|	[hex]

	def lm_act
		['=>' action_ref]
	|	[action_block]

	def lm_stmt
		[join lm_act? ';'] commit
	|	[assignment]
	|	[action_spec]

	def lm
		[Join: join] :Join
	|	['|*' LmStmtList: lm_stmt+ '*|'] :Lm

	def action_spec
		['action' Name: word ActionBlock: action_block] commit

	def assignment
		[Name: word '=' Join: join ';'] commit

	def instantiation
		[Name: word ':=' Lm: lm ';'] commit

	def statement
		['machine' MachineName: word ';'] :MachineName commit
	|	[Assignment: assignment] :Assignment
	|	[Instantiation: instantiation] :Instantiation
	|	[ActionSpec: action_spec] :ActionSpec
	|	['prepush' PrePushBlock: action_block] :PrePushSpec commit
	|	['postpop' PostPopBlock: action_block] :PostPopSpec commit
	|	['variable' Var: word Reparse: inline_expr_reparse] :Variable commit
	|	['alphtype' inline_expr_reparse] commit
	|	['access' inline_expr_reparse] commit
	|	['write' Cmd: word ArgList: word* ';'] :Write commit
	|	['getkey' inline_expr_reparse] commit
	|	['import' inline_expr_reparse] commit
	|	['export' inline_expr_reparse] commit
	|	['include' inline_expr_reparse] commit

	def ragel_start 
		[statement*]
end

namespace c_host
	lex 
		literal '%%{', '}%%'

		token slr /'%%' [^{] [^\n]* '\n'/
		{
			input.pull( 2 )
			R: str = input.pull( match_length - 3 )
			input.push( "}%%" )
			input.push( R )
			input.push( "%%{" )
		}

		rl NL / '\n' /

		rl ruby_comment / '#' [^\n]* NL /

		rl s_literal
			/ "'" ([^'\\] | NL | '\\' (any | NL))* "'" /

		rl d_literal
			/ '"' ([^"\\] | NL | '\\' (any | NL))* '"' /

		rl host_re_literal
			/ '/' ([^/\\] | NL | '\\' (any | NL))* '/' /

		token ident /ident/
		token number /digit+/
		token hex_number /'0x' [0-9a-fA-F]+/

		token comment
			/c_comment | cpp_comment/

		token string
			/ s_literal | d_literal /

		token whitespace
			/ ( [ \t] | NL )+ /

		token c_any / any /
	end

	def tok
		[ident]
	|	[number]
	|	[hex_number]
	|	[comment]
	|	[string]
	|	[whitespace]
	|	[c_any]

	def section
		['%%{' StmtList: ragel::statement* '}%%'] :MultiLine
	|	[Tok: tok] :Tok
end

